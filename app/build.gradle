apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

static def gitCommand() {

    def gitResult = "git describe --tags".execute().text.split("-")
    def regexResult = gitResult[0].findAll("(\\d+)")
    def versionMajor = regexResult[0].toInteger()
    def versionMinor = regexResult[1].toInteger()
    def versionPatch = gitResult[1].toInteger()

    return [versionMajor, versionMinor, versionPatch]
}

static def computeVersionName() {
    def ver = gitCommand()
    return "${ver[0]}.${ver[1]}.${ver[2]}"
}

static def computeVersionCode() {
    def ver = gitCommand()
    return 1000 * ver[0] + 100 * ver[1] + 10 * ver[2]
}

def getSigningPasswords() {
    def lines = file("${System.properties['user.home']}${File.separator}.android${File.separator}melamoryReleasePasswords.txt").readLines()
    return lines
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.enchantme.akali"
        manifestPlaceholders = [orientation: "portrait"]
        minSdkVersion 23
        targetSdkVersion 28
        versionCode computeVersionCode()
        versionName computeVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file("${System.properties['user.home']}${File.separator}.android${File.separator}melamoryRelease.jks")
            storePassword getSigningPasswords()[0]
            keyAlias "melamory"
            keyPassword getSigningPasswords()[1]
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "${applicationId}-${variant.versionName}.apk"
                }
            }
        }
    }

    flavorDimensions "version"
    productFlavors {
        prod {
            dimension "version"
            minSdkVersion 23
            applicationId 'com.enchantme.akali'
            targetSdkVersion 28
            testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        }
        dev {
            manifestPlaceholders = [orientation: "unspecified"]
            dimension "version"
            versionNameSuffix "-dev"
            minSdkVersion 23
            applicationId 'com.enchantme.akali'
            targetSdkVersion 28
            testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    def nav_version = "1.0.0-alpha07"

    implementation 'com.github.esafirm.android-image-picker:imagepicker:1.13.1'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    implementation "android.arch.lifecycle:extensions:1.1.1"
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"
    implementation "android.arch.navigation:navigation-fragment:$nav_version" // use -ktx for Kotlin
    implementation "android.arch.navigation:navigation-ui:$nav_version" // use -ktx for Kotlin
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation 'com.google.firebase:firebase-core:16.0.5'
    implementation 'com.google.firebase:firebase-auth:16.0.5'
}
